  #  Copyright (C) CFEngine AS

  #  This file is part of CFEngine 3 - written and maintained by CFEngine AS.

  #  This program is free software; you can redistribute it and/or modify it
  #  under the terms of the GNU General Public License as published by the
  #  Free Software Foundation; version 3.

  #  This program is distributed in the hope that it will be useful,
  #  but WITHOUT ANY WARRANTY; without even the implied warranty of
  #  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
  #  GNU General Public License for more details.

  # You should have received a copy of the GNU General Public License
  # along with this program; if not, write to the Free Software
  # Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA

  # To the extent this program is licensed as part of the Enterprise
  # versions of CFEngine, the applicable Commerical Open Source License
  # (COSL) may apply to this file if you as a licensee so wish it. See
  # included file COSL.txt.

# Math Grammar

Expr	<- SPACE Sum	        { yy->result = pop(); }
	 / .		{ strcpy(yy->failure, "expression could not be parsed"); }

Sum	<- Product ( PLUS  Product	{ double r= pop(), l= pop();  push(l + r); }
		   / MINUS Product	{ double r= pop(), l= pop();  push(l - r); }
		   / CLOSE_ENOUGH Product	{ double r= pop(), l= pop();  push(fabs(l - r) < 0.00000000000000001); }
		   )*

Product	<- Value ( POW  Value		{ double r= pop(), l= pop();  push(pow(l, r)); }
                 / TIMES  Value		{ double r= pop(), l= pop();  push(l * r); }
                 / DIVIDE Value		{ double r= pop(), l= pop();  push(l / r); }
                 / MOD Value		{ double r= pop(), l= pop();  push((long)l % (long)r); }
		 )*

Value	<- F_NUMBER			{ double scanned = 0; sscanf(yytext, "%lf", &scanned); push(scanned); /*Log(LOG_LEVEL_ERR, "YY: read FP %lf", scanned);*/ }
         / Constant
         / Funcall
	 / OPEN Sum CLOSE

Funcall <- Fname OPEN Value CLOSE { push(EvaluateMathFunction(yy->fname, pop())); }

Fname <- < ( "ceil" / "floor" / "log10" / "log2" / "log" / "sqrt" / "sin" / "cos" / "tan" / "asin" / "acos" / "atan" / "abs" / "step" ) > { strcpy(yy->fname, yytext); }

Constant <- "e" { push(2.7182818284590452354); }
         / "log2e" { push(1.4426950408889634074); }
         / "log10e" { push(0.43429448190325182765); }
         / "ln2" { push(0.69314718055994530942); }
         / "ln10" { push(2.30258509299404568402); }
         / "pi" { push(3.14159265358979323846); }
         / "pi_2" { push(1.57079632679489661923); }
         / "pi_4" { push(0.78539816339744830962); }
         / "1_pi" { push(0.31830988618379067154); }
         / "2_pi" { push(0.63661977236758134308); }
         / "2_sqrtpi" { push(1.12837916709551257390); }
         / "sqrt2" { push(1.41421356237309504880); }
         / "sqrt1_2" { push(0.70710678118654752440); }

# Lexemes

F_NUMBER <- < '-'? [0-9]+ '.'? [0-9]* > SPACE
CLOSE_ENOUGH	<- '==' SPACE
PLUS	<- '+' SPACE
MINUS	<- '-' SPACE
TIMES	<- '*' SPACE
DIVIDE	<- '/' SPACE
MOD	<- '%' SPACE
POW	<- '^' SPACE / '**' SPACE
OPEN	<- '(' SPACE
CLOSE	<- ')' SPACE
SPACE	<- [ \t]*
